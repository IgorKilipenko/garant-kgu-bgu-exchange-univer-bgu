// Гарант+ Килипенко 24.04.2025 [] Обмен Университет-БГУ ++ {
#Область ПрограммныйИнтерфейс

Функция ПолучитьСервисОбмена() Экспорт
    ЭтоБазаБГУ = ЭтоБазаБГУ();
    ЭтоБазаУниверситет = (НЕ ЭтоБазаБГУ) И ЭтоБазаУниверситет();

    Если ЭтоБазаБГУ Тогда
        Возврат СервисыИнтеграции.ГП_СервисИнтеграцииБГУУниверситет;
    ИначеЕсли ЭтоБазаУниверситет Тогда
        Возврат СервисыИнтеграции.ГП_СервисИнтеграцииУниверситетБГУ;
    Иначе
        ВызватьИсключение(СтрШаблон("База << %1 >> не поддерживается сервисом обмена << (Гарант+) Университет-БГУ >>",
                ОбменДаннымиПовтИсп.ИмяЭтойИнформационнойБазы()));
    КонецЕсли;
КонецФункции

// Возвращаемое значение:
//  - Булево
Функция ЭтоБазаБГУ() Экспорт
    Возврат ГП_ОбменУниверситетБГУПовтИсп.ЭтоБазаБГУ();
КонецФункции

// Возвращаемое значение:
//  - Булево
Функция ЭтоБазаУниверситет() Экспорт
    Возврат ГП_ОбменУниверситетБГУПовтИсп.ЭтоБазаУниверситет();
КонецФункции

// Возвращаемое значение:
//  - Строка
Функция ПолучитьНаименованиеСервисаОбменаУниверситет() Экспорт
    Возврат СтандартныеПодсистемыВызовСервера.ИмяОбъектаМетаданных(ТипЗнч(ПолучитьСервисОбмена()));
КонецФункции

#Область БГУ

// Параметры:
//  ТестовыйРежим - Булево
//  Сервис - СервисИнтеграции
Функция ПолучитьКаналОбменаВУниверситет(Знач ТестовыйРежим, Знач Сервис = Неопределено) Экспорт
    Если ЭтоБазаБГУ() = Ложь Тогда
        ВызватьИсключение("Канал ""ВУниверситет"" поддерживается только базой БГУ");
    КонецЕсли;

    СервисИнтеграции = ?(Сервис = Неопределено, ПолучитьСервисОбмена(), Сервис);
    Возврат СервисИнтеграции[ГП_ОбменУниверситетБГУКлиентСервер.ПолучитьИмяКаналаОбменаВУниверситет(ТестовыйРежим)];
КонецФункции

// Параметры:
//  ТестовыйРежим - Булево
//  Сервис - СервисИнтеграции
Функция ПолучитьКаналОбменаИзУниверситета(Знач ТестовыйРежим, Знач Сервис = Неопределено) Экспорт
    Если ЭтоБазаБГУ() = Ложь Тогда
        ВызватьИсключение("Канал ""ИзУниверситета"" поддерживается только базой БГУ");
    КонецЕсли;

    СервисИнтеграции = ?(Сервис = Неопределено, ПолучитьСервисОбмена(), Сервис);
    Возврат СервисИнтеграции[ГП_ОбменУниверситетБГУКлиентСервер.ПолучитьИмяКаналаОбменаИзУниверситета(ТестовыйРежим)];
КонецФункции

// Параметры:
//  СтруктураСообщения - Структура
//      * Параметры - Структура - Опционально
//      * СтруктураТела - Структура - Опционально
//  СервисОбмена - СервисИнтеграции
// Возвращаемое значение:
//  - СообщениеСервисаИнтеграции
Функция НовыйСообщениеВУниверситет(Знач СтруктураСообщения, Знач СервисОбмена) Экспорт
    КодПолучателя = ГП_ОбменУниверситетБГУКлиентСервер.ПолучитьКодБазыУниверситет();
    Сообщение = НовыйСообщениеИзБухгалтерии(СтруктураСообщения, КодПолучателя, СервисОбмена);

    Возврат Сообщение;
КонецФункции

// Параметры:
//  СтруктураСообщения - Структура
//      * Параметры - Структура - Опционально
//      * СтруктураТела - Структура - Опционально
//  КодПолучателя - Строка
//  СервисОбмена - СервисИнтеграции
// Возвращаемое значение:
//  - СообщениеСервисаИнтеграции
Функция НовыйСообщениеИзБухгалтерии(Знач СтруктураСообщения, Знач КодПолучателя, Знач СервисОбмена) Экспорт
    Сообщение = ГП_СервисыИнтеграции.НовыйСообщениеСервисаИнтеграции(СтруктураСообщения, СервисОбмена);
    Сообщение.КодПолучателя = КодПолучателя;
    Сообщение.КодОтправителя = ГП_ОбменУниверситетБГУКлиентСервер.ПолучитьКодБазыБГУ();

    Возврат Сообщение;
КонецФункции

// Параметры:
//  ИдентификаторСообщенияЗапроса - УникальныйИдентификатор, Неопределено
// Возвращаемое значение:
//  - Структура
//      * КодПолучателя - Строка
//      * КодОтправителя - Строка
//      * ИдентификаторСообщенияЗапроса - УникальныйИдентификатор
Функция НовыйПараметрыОтбораСообщенийОтветаУниверситета(
        Знач ИдентификаторСообщенияЗапроса = Неопределено) Экспорт

    ПараметрыОтбора = ГП_СервисыИнтеграции.НовыйПараметрыОтбораСообщенийОтвета(ИдентификаторСообщенияЗапроса);
    ПараметрыОтбора.КодПолучателя = ГП_ОбменУниверситетБГУКлиентСервер.ПолучитьКодБазыБГУ();
    ПараметрыОтбора.КодОтправителя = ГП_ОбменУниверситетБГУКлиентСервер.ПолучитьКодБазыУниверситет();

    Возврат ПараметрыОтбора;
КонецФункции

// Параметры:
//  МассивСообщений - Массив из СообщениеСервисаИнтеграции
//  ТестовыйРежим - Булево
//  КаналОбменаВУниверситет - КаналСервисаИнтеграции
//  СервисОбмена - СервисИнтеграции
// Возвращаемое значение:
//  - Структура
//      * Успех - Булево
//      * ТекстСообщения - Строка, Неопределено
//      * СообщенияДобавленыВКанал - Булево
//      * ИдентификаторыОтправленныхСообщений - Массив из УникальныйИдентификатор
Функция ПопыткаОтправитьКоллекциюСообщенийВУниверситет(
        Знач МассивСообщений, Знач ТестовыйРежим, Знач КаналОбменаВУниверситет = Неопределено, Знач СервисОбмена = Неопределено) Экспорт

    РезультатФункции = Новый Структура(
            "Успех, СообщенияДобавленыВКанал, ИдентификаторыОтправленныхСообщений, ТекстСообщения", Истина, Ложь, Новый Массив);

    Попытка
        Если КаналОбменаВУниверситет = Неопределено Тогда
            КаналОбменаВУниверситет = ПолучитьКаналОбменаВУниверситет(ТестовыйРежим, СервисОбмена);
        КонецЕсли;

        Для Каждого Сообщение Из МассивСообщений Цикл
            КаналОбменаВУниверситет.ОтправитьСообщение(Сообщение);
            РезультатФункции.ИдентификаторыОтправленныхСообщений.Добавить(Сообщение.Идентификатор);
        КонецЦикла;

        РезультатФункции.СообщенияДобавленыВКанал = РезультатФункции.ИдентификаторыОтправленныхСообщений.Количество() > 0;

    Исключение
        РезультатФункции.Успех = Ложь;
        ИнформацияОбОшибке = ИнформацияОбОшибке();
        РезультатФункции.ТекстСообщения = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
    КонецПопытки;

    Если РезультатФункции.Успех И РезультатФункции.СообщенияДобавленыВКанал Тогда
        // Отправка данных
        Попытка
            ГП_СервисыИнтеграции.ВыполнитьОбработкуСервисовИнтеграции(Истина);
        Исключение
            РезультатФункции.Успех = Ложь;
            ИнформацияОбОшибке = ИнформацияОбОшибке();
            РезультатФункции.ТекстСообщения = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
            ЗаписьЖурналаРегистрации(ГП_ОбменУниверситетБГУКлиентСервер.ПолучитьИмяСобытияДляЖурнала(),
                УровеньЖурналаРегистрации.Ошибка, , ,
                РезультатФункции.ТекстСообщения);
        КонецПопытки;
    КонецЕсли;

    Возврат РезультатФункции;
КонецФункции

// Параметры:
//  РаботаВТестовомРежиме - Булево
//  АдресРезультата - Строка, Неопределено
// Возвращаемое значение:
//  - Структура, Неопределено - Неопределено если заполнен аргумент `АдресРезультата`
//      * Успех - Булево
//      * ТекстСообщения - Строка, Неопределено
Функция ПроверитьСоединениеСБазойУниверситет(Знач РаботаВТестовомРежиме, Знач АдресРезультата = Неопределено) Экспорт
    РезультатФункции = Новый Структура("Успех, ТекстСообщения, АдресРезультата", Истина);

    СервисОбмена = ПолучитьСервисОбмена();
    РезультатПроверкиСервиса = ГП_СервисыИнтеграции.ПроверитьДоступностьСервиса(СервисОбмена);
    Если НЕ РезультатПроверкиСервиса.Успех Тогда
        РезультатФункции.Успех = Ложь;
        РезультатФункции.ТекстСообщения = СтрШаблон(
                "Ошибка проверки статуса соединения.
                |Информация: %1", РезультатПроверкиСервиса.ТекстСообщения);

    Иначе
        // Отправка сообщения
        СтруктураСообщения = Новый Структура("Параметры", Новый Структура);
        СтруктураСообщения.Параметры.Вставить("ТипСообщения", "ПроверкаСоединения");
        СообщениеОбмена = ГП_ОбменУниверситетБГУ.НовыйСообщениеВУниверситет(СтруктураСообщения, СервисОбмена);
        КаналОбменаВУниверситет = ГП_ОбменУниверситетБГУ.ПолучитьКаналОбменаВУниверситет(РаботаВТестовомРежиме, СервисОбмена);
        КаналОбменаИзУниверситета = ГП_ОбменУниверситетБГУ.ПолучитьКаналОбменаИзУниверситета(РаботаВТестовомРежиме, СервисОбмена);

        РезультатОтправкиСообщения = ГП_ОбменУниверситетБГУ.ПопыткаОтправитьКоллекциюСообщенийВУниверситет(
                ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СообщениеОбмена),
                РаботаВТестовомРежиме,
                КаналОбменаВУниверситет);

        Если РезультатОтправкиСообщения.Успех = Ложь Тогда
            РезультатФункции.Успех = Ложь;
            РезультатФункции.ТекстСообщения = РезультатОтправкиСообщения.ТекстСообщения;

        ИначеЕсли РезультатОтправкиСообщения.ИдентификаторыОтправленныхСообщений.Количество() > 0 Тогда
            ИдентификаторСообщенияЗапроса = РезультатОтправкиСообщения.ИдентификаторыОтправленныхСообщений[0];

            // Получение ответа
            ПараметрыОтбора = НовыйПараметрыОтбораСообщенийОтветаУниверситета(ИдентификаторСообщенияЗапроса);
            МассивСообщенийОтвета = ГП_ОбменУниверситетБГУ.ПолучитьСообщенияСервиса(
                    КаналОбменаИзУниверситета, ПараметрыОтбора, 5000);

            Если МассивСообщенийОтвета.Количество() = 0 Тогда
                РезультатФункции.Успех = Ложь;
                РезультатФункции.ТекстСообщения = СтрШаблон("Соединение не установлено");
            КонецЕсли;
        КонецЕсли;
    КонецЕсли;

    Если АдресРезультата <> Неопределено Тогда
        ПоместитьВоВременноеХранилище(РезультатФункции, АдресРезультата);
        РезультатФункции = Неопределено; // Помещено в хранилище
    КонецЕсли;

    Возврат РезультатФункции;
КонецФункции

#КонецОбласти // БГУ

#Область Университет

// Параметры:
//  ТестовыйРежим - Булево
//  Сервис - СервисИнтеграции
Функция ПолучитьКаналОбменаВБухгалтерию(Знач ТестовыйРежим, Знач Сервис = Неопределено) Экспорт
    Если ЭтоБазаУниверситет() = Ложь Тогда
        ВызватьИсключение("Канал ""ВБухгалтерию"" поддерживается только базой БГУ");
    КонецЕсли;

    СервисИнтеграции = ?(Сервис = Неопределено, ПолучитьСервисОбмена(), Сервис);
    Возврат СервисИнтеграции[ГП_ОбменУниверситетБГУКлиентСервер.ПолучитьИмяКаналаОбменаВБухгалтерию(ТестовыйРежим)];
КонецФункции

// Параметры:
//  ТестовыйРежим - Булево
//  Сервис - СервисИнтеграции
Функция ПолучитьКаналОбменаИзБухгалтерии(Знач ТестовыйРежим, Знач Сервис = Неопределено) Экспорт
    Если ЭтоБазаУниверситет() = Ложь Тогда
        ВызватьИсключение("Канал ""ИзБухгалтерии"" поддерживается только базой БГУ");
    КонецЕсли;

    СервисИнтеграции = ?(Сервис = Неопределено, ПолучитьСервисОбмена(), Сервис);
    Возврат СервисИнтеграции[ГП_ОбменУниверситетБГУКлиентСервер.ПолучитьИмяКаналаОбменаИзБухгалтерии(ТестовыйРежим)];
КонецФункции

// Параметры:
//  СтруктураСообщения - Структура
//      * Параметры - Структура - Опционально
//      * СтруктураТела - Структура - Опционально
//  СервисОбмена - СервисИнтеграции
// Возвращаемое значение:
//  - СообщениеСервисаИнтеграции
Функция НовыйСообщениеВБухгалтерию(Знач СтруктураСообщения, Знач СервисОбмена) Экспорт
    КодПолучателя = ГП_ОбменУниверситетБГУКлиентСервер.ПолучитьКодБазыБГУ();
    Сообщение = НовыйСообщениеИзУниверситета(СтруктураСообщения, КодПолучателя, СервисОбмена);

    Возврат Сообщение;
КонецФункции

// Параметры:
//  СтруктураСообщения - Структура
//      * Параметры - Структура - Опционально
//      * СтруктураТела - Структура - Опционально
//  КодПолучателя - Строка
//  СервисОбмена - СервисИнтеграции
// Возвращаемое значение:
//  - СообщениеСервисаИнтеграции
Функция НовыйСообщениеИзУниверситета(Знач СтруктураСообщения, Знач КодПолучателя, Знач СервисОбмена) Экспорт
    Сообщение = ГП_СервисыИнтеграции.НовыйСообщениеСервисаИнтеграции(СтруктураСообщения, СервисОбмена);
    Сообщение.КодПолучателя = КодПолучателя;
    Сообщение.КодОтправителя = ГП_ОбменУниверситетБГУКлиентСервер.ПолучитьКодБазыУниверситет();

    Возврат Сообщение;
КонецФункции

// Параметры:
//  ИдентификаторСообщенияЗапроса - УникальныйИдентификатор, Неопределено
// Возвращаемое значение:
//  - Структура
//      * КодПолучателя - Строка
//      * КодОтправителя - Строка
//      * ИдентификаторСообщенияЗапроса - УникальныйИдентификатор
Функция НовыйПараметрыОтбораСообщенийОтветаБухгалтерии(Знач ИдентификаторСообщенияЗапроса = Неопределено) Экспорт
    ПараметрыОтбора = ГП_СервисыИнтеграции.НовыйПараметрыОтбораСообщенийОтвета(ИдентификаторСообщенияЗапроса);
    ПараметрыОтбора.КодПолучателя = ГП_ОбменУниверситетБГУКлиентСервер.ПолучитьКодБазыУниверситет();
    ПараметрыОтбора.КодОтправителя = ГП_ОбменУниверситетБГУКлиентСервер.ПолучитьКодБазыБГУ();

    Возврат ПараметрыОтбора;
КонецФункции

#КонецОбласти // Университет

// Параметры:
//  КаналОбмена
//  ПараметрыОтбора - Структура
//      * КодПолучателя - Строка
//      * КодОтправителя - Строка
//      * ИдентификаторСообщенияЗапроса - УникальныйИдентификатор
//      * ДатаОтправкиЗапроса - Дата
//  ЗадержкаМсек - Число, Неопределено - По умолчанию 1000 мс
// Возвращаемое значение:
//  - Массив из Структура
//      * ДатаОтправки - Дата
//      * ДатаУстаревания - Дата
//      * Идентификатор - УникальныйИдентификатор
//      * ИдентификаторСообщенияЗапроса - УникальныйИдентификатор
//      * КодОтправителя - Строка
//      * КодПолучателя - Строка
//      * Параметры - Соответствие
//      * ПредставлениеТелаСообщения - Строка
Функция ПолучитьСообщенияСервиса(КаналОбмена, Знач ПараметрыОтбора, Знач ЗадержкаМсек = Неопределено) Экспорт
    Возврат ГП_СервисыИнтеграции.ПолучитьСообщенияСервиса(КаналОбмена, ПараметрыОтбора, ЗадержкаМсек);
КонецФункции

// Параметры:
//  ТелоСообщенияJSON - Строка
//  ПараметрыПреобразования - Структура, Неопределено
// Возвращаемое значение:
//  - Произвольный
Функция ПолучитьЗначениеТелаСообщенияУниверситетНаСервере(Знач ТелоСообщенияJSON) Экспорт
    РезультатФункции = ПрочитатьЗначениеJSON(ТелоСообщенияJSON);

    ПараметрыПреобразования = ?(ПараметрыПреобразования = Неопределено,
            Новый Структура("ПреобразоватьДатуВМестную", Ложь), ПараметрыПреобразования);

    РезультатФункции = ПреобразоватьЗначенияПрочитанноеИзJSONРекурсивно(
            РезультатФункции, ПараметрыПреобразования);

    Возврат РезультатФункции;
КонецФункции

// Возвращаемое значение:
//  - СправочникСсылка.Организации
Функция ПолучитьОрганизациюПоУмолчанию() Экспорт
    Возврат Справочники.Организации.ОрганизацияПоУмолчанию();
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс
// Гарант+ Килипенко 24.04.2025 [] Обмен Университет-БГУ -- }

#Область СлужебныеПроцедурыИФункции

// Параметры:
//  Значение - Произвольный
//  ПараметрыПреобразования - Структура, Неопределено
// Возвращаемое значение:
//  - Произвольный
Функция ПреобразоватьЗначенияПрочитанноеИзJSONРекурсивно(Знач Значение, Знач ПараметрыПреобразования = Неопределено) Экспорт
    РезультатФункции = Значение;

    ПараметрыПреобразования = ?(ПараметрыПреобразования = Неопределено,
            Новый Структура("ПреобразоватьДатуВМестную", Ложь), ПараметрыПреобразования);

    Если ТипЗнч(РезультатФункции) = Тип("Структура") ИЛИ ТипЗнч(РезультатФункции) = Тип("Соответствие") Тогда
        Для Каждого ЭлементКЗ Из РезультатФункции Цикл
            РезультатФункции[ЭлементКЗ.Ключ] = ПреобразоватьЗначенияПрочитанноеИзJSONРекурсивно(
                    ЭлементКЗ.Значение, ПараметрыПреобразования);
        КонецЦикла;
    ИначеЕсли ТипЗнч(РезультатФункции) = Тип("Массив") Тогда
        Для ИндексЭлемента = 0 По РезультатФункции.Количество() - 1 Цикл
            РезультатФункции[ИндексЭлемента] = ПреобразоватьЗначенияПрочитанноеИзJSONРекурсивно(
                    РезультатФункции[ИндексЭлемента], ПараметрыПреобразования);
        КонецЦикла;
    Иначе
        РезультатФункции = ПреобразоватьЗначениеПрочитанноеИзJSON(РезультатФункции, ПараметрыПреобразования);
    КонецЕсли;

    Возврат РезультатФункции;
КонецФункции

// Параметры:
//  Значение - Произвольный
//  ПараметрыПреобразования - Структура
//      * ПреобразоватьДатуВМестную - Булево
// Возвращаемое значение:
//  - Произвольный
Функция ПреобразоватьЗначениеПрочитанноеИзJSON(Знач Значение, Знач ПараметрыПреобразования)
    РезультатФункции = Значение;

    ЭтоДата = Ложь;

    Если ТипЗнч(Значение) = Тип("Строка") Тогда
        ЭтоДата = Истина;
        Если СтрНачинаетсяС(Значение, "new Date(") И СтрЗаканчиваетсяНа(Значение, ")") Тогда // Это дата в формате JS
            РезультатФункции = ПрочитатьДатуJSON(Значение, ФорматДатыJSON.JavaScript);
        ИначеЕсли СтрНачинаетсяС(Значение, "/Date(") И СтрЗаканчиваетсяНа(Значение, ")") Тогда
            РезультатФункции = ПрочитатьДатуJSON(Значение, ФорматДатыJSON.Microsoft);
        Иначе
            ЭтоДата = Ложь;
        КонецЕсли;
    КонецЕсли;

    // Преобразование даты
    Если ЭтоДата = Истина И ПараметрыПреобразования.ПреобразоватьДатуВМестную Тогда
        ВызватьИсключение("Преобразование в местное время временно запрещено.");

        // РезультатФункции = ГП_СервисыИнтеграции.ПреобразоватьВМестнуюДатуСеанса(РезультатФункции);
    КонецЕсли;

    Возврат РезультатФункции;
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
